//open and close systemcall
alias userSP R0;
userSP = SP;

alias process_entry R1;
process_entry = PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16;

[process_entry + 13] = SP;

SP = [process_entry + 11]*512 -1;

alias syscall R2;
syscall = [[PTBR + 2*(userSP - 5)/512]*512 + (userSP-5)%512];

//open syscall
if(syscall == 2) then
   


   [process_entry + 9] = 2;

   alias file_name R3;
   alias user_page R4;
   alias RESOURCE_TABLE_ADDRESS R5;
   alias i R6;

   file_name =  [[PTBR + 2*(userSP - 4)/512]*512 + (userSP-4)%512];
   user_page = [process_entry + 11];
   RESOURCE_TABLE_ADDRESS = (user_page + 1)*512 - 16;


   i = 0;
   while(i<8) do
      
      if([RESOURCE_TABLE_ADDRESS + i*2] == -1) then
          
          break;

      endif;

      i = i+1;

    endwhile;

    //Process has reached its limit of resources
    if(i == 8) then
       
      R10 = -3;
      goto open_ret;

    endif;



    multipush(R0,R1,R2,R3,R4,R5,R6);
    R1 = 3;
    R2 = file_name;
    call MOD_3;                         //file manager
    R7 = R0; 
    multipop(R0,R1,R2,R3,R4,R5,R6);

    alias open_file_entry R7;



    //File not found or file is not a data or root file
    if(open_file_entry == -1) then
         
       R10 = -1;
       goto open_ret;
    
    endif;
    

    // System has reached its limit of open files
    if(open_file_entry == -2) then
       
       R10 = -2;
       goto open_ret;

    endif;

    [RESOURCE_TABLE_ADDRESS + i*2] = 0;
    [RESOURCE_TABLE_ADDRESS + i*2 + 1] = open_file_entry;

    
    R10 = i;

open_ret :
    
    [process_entry + 9] =0;
    [[PTBR + 2*(userSP - 1)/512]*512 + (userSP-1)%512] = R10;
    SP = [process_entry + 13];
    ireturn;
     
endif;


//close syscall
if(syscall == 3) then
     
     [process_entry + 9] =3;

     alias file_descriptor R3;
     alias user_page R4;
     alias RESOURCE_TABLE_ADDRESS R5;

     file_descriptor = [[PTBR + 2*(userSP - 4)/512]*512 + (userSP-4)%512];

     user_page = [process_entry + 11]; 

     RESOURCE_TABLE_ADDRESS = (user_page + 1)*512 - 16;

     if([RESOURCE_TABLE_ADDRESS + 2*file_descriptor] != 0) then
         
           R10 = -1;
           goto close_ret; 

    endif;


    multipush(R0,R1,R2,R3,R4,R5);
    R1 = 4;
    R2 = [RESOURCE_TABLE_ADDRESS + 2*file_descriptor + 1];
    call MOD_3;
    multipop(R0,R1,R2,R3,R4,R5);


    [RESOURCE_TABLE_ADDRESS + 2*file_descriptor] = -1;
    [RESOURCE_TABLE_ADDRESS + 2*file_descriptor + 1] = -1;


    R10 = 0;

close_ret :
          
           [process_entry + 9] =0;
           [[PTBR + 2*(userSP - 1)/512]*512 + (userSP-1)%512] = R10;
           SP = [process_entry + 13];
           ireturn;
    
endif;











   
